/PROG  HF_MAIN
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "MAIN";
PROG_SIZE	= 2012;
CREATE		= DATE 20-06-29  TIME 11:05:56;
MODIFIED	= DATE 21-04-30  TIME 15:51:38;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 109;
MEMORY_SIZE	= 2512;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/MN
   1:  !******************************** ;
   2:  !Standard 8.1.2 [pending] ;
   3:  !******************************** ;
   4:   ;
   5:   ;
   6:  !PLC BYPASS TESTING ONLY ;
   7:  //IF R[399:PLC_BYPASS]=1,JMP LBL[30] ;
   8:   ;
   9:  !CLEARING STATE ;
  10:  LBL[10:Initialize] ;
  11:  R[242:ROBOT_STATE]=600    ;
  12:  GO[11]=R[242:ROBOT_STATE] ;
  13:   ;
  14:   ;
  15:  CALL HF_INITIALIZE    ;
  16:  CALL HF_RECOV    ;
  17:   ;
  18:   ;
  19:  !STOPPED STATE ;
  20:  R[242:ROBOT_STATE]=105    ;
  21:  GO[11]=R[242:ROBOT_STATE] ;
  22:   ;
  23:  //PAUSE ;
  24:   ;
  25:  !EXECUTE STATE ;
  26:  LBL[20:Task Loop] ;
  27:  R[242:ROBOT_STATE]=305    ;
  28:  GO[11]=R[242:ROBOT_STATE] ;
  29:   ;
  30:   ;
  31:   ;
  32:   ;
  33:  !Enter Executing Loop ;
  34:  LBL[21:Set Trans ID] ;
  35:   ;
  36:  !Request To Enter @PostPick ;
  37:  IF DI[81:RTE]=OFF,JMP LBL[22] ;
  38:   ;
  39:  !Move Home if RTE Active ;
  40:  UFRAME_NUM=0 ;
  41:L PR[200:HOME] 2500mm/sec FINE    ;
  42:  R[250:CUR_POS]=1000    ;
  43:   ;
  44:  !CLEAR OF ALL ;
  45:  R[241:LOC_STATUS]=9999    ;
  46:  GO[10]=R[241:LOC_STATUS] ;
  47:  LBL[22] ;
  48:   ;
  49:  TIMER[10]=STOP ;
  50:  TIMER[10]=RESET ;
  51:  TIMER[10]=START ;
  52:   ;
  53:  !Check for New Trnsct From PLC ;
  54:  R[201:TRANS_ID]=GI[2:TRANS ID]    ;
  55:   ;
  56:  !Simulation Only ;
  57:  //R[201:TRANS_ID]=R[201:TRANS_ID]+1    ;
  58:   ;
  59:  IF R[201:TRANS_ID]=0,JMP LBL[21] ;
  60:  IF R[201:TRANS_ID]=R[226:ACK_TRANS_ID],JMP LBL[21] ;
  61:  IF R[201:TRANS_ID]<>R[226:ACK_TRANS_ID],JMP LBL[30] ;
  62:   ;
  63:  !Read Data from PLC ;
  64:  LBL[30:Execution Loop] ;
  65:  IF R[399:PLC_BYPASS]=0,CALL HF_GETDATA ;
  66:   ;
  67:  !Select Valid Command for App ;
  68:  SELECT R[202:CMD_ID]=50,CALL HF_50_PKPL ;
  69:         =900,CALL HF_900_MOV_ROBOT ;
  70:         ELSE,JMP LBL[9001] ;
  71:   ;
  72:   ;
  73:   ;
  74:  !Finished Task - Send Resonses ;
  75:  LBL[35:Task Completion] ;
  76:   ;
  77:  R[226:ACK_TRANS_ID]=R[201:TRANS_ID]    ;
  78:  GO[2:Ack Trans ID]=R[226:ACK_TRANS_ID] ;
  79:   ;
  80:  R[227:ACK_CMD_ID]=R[202:CMD_ID]+1000    ;
  81:  GO[3:Ack Cmd ID]=R[227:ACK_CMD_ID] ;
  82:   ;
  83:  R[228:TASK_RSPND]=1    ;
  84:  GO[4:Task response]=R[228:TASK_RSPND] ;
  85:   ;
  86:  !Reset Robot Warnings ;
  87:  R[240:ERROR_STATE]=0    ;
  88:  GO[9]=R[240:ERROR_STATE] ;
  89:   ;
  90:  TIMER[10]=STOP ;
  91:  R[191:Cycle Timer]=TIMER[10]    ;
  92:   ;
  93:  !Back to top ;
  94:  JMP LBL[20] ;
  95:   ;
  96:   ;
  97:   ;
  98:   ;
  99:  !Invalid Command Must Abort ;
 100:  LBL[9001] ;
 101:  R[240:ERROR_STATE]=1    ;
 102:  JMP LBL[9000] ;
 103:   ;
 104:  !Error Loop ;
 105:  LBL[9000] ;
 106:  CALL HF_ERRORLOOP    ;
 107:  JMP LBL[20] ;
 108:   ;
 109:  LBL[9999:End] ;
/POS
/END
