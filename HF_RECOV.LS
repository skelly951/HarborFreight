/PROG  HF_RECOV
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "RECOVER/ GO HOME";
PROG_SIZE	= 3210;
CREATE		= DATE 20-06-29  TIME 11:05:46;
MODIFIED	= DATE 22-05-02  TIME 23:21:50;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 190;
MEMORY_SIZE	= 3750;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,1,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/MN
   1:  OVERRIDE=R[255:SPEED_OVERRIDE] ;
   2:   ;
   3:  R[257:VEL SETTING]=R[256:RECOVER_OVERRIDE]/100*2500    ;
   4:  R[258:ACC SETTING]=R[256:RECOVER_OVERRIDE]/100    ;
   5:   ;
   6:   ;
   7:  !Setup Payload & Frames ;
   8:  PAYLOAD[1:Vacuum Gripper] ;
   9:  UFRAME_NUM=0 ;
  10:  UTOOL_NUM=1 ;
  11:   ;
  12:   ;
  13:   ;
  14:  !***************************** ;
  15:  !Move to Position State ;
  16:  !***************************** ;
  17:   ;
  18:  JMP LBL[R[250]] ;
  19:   ;
  20:   ;
  21:   ;
  22:  !***************************** ;
  23:  !Move Home from Pre-Pick ;
  24:  !***************************** ;
  25:   ;
  26:  !@FNL AP PK ;
  27:  LBL[110] ;
  28:  UFRAME_NUM=1 ;
  29:  CALL HF_VAC_OFF    ;
  30:L PR[223:FINAL APP PK] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  31:  R[250:CUR_POS]=110    ;
  32:   ;
  33:  !@INIT AP PK ;
  34:  LBL[105] ;
  35:  UFRAME_NUM=1 ;
  36:L PR[220:INIT APP PK] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  37:  R[250:CUR_POS]=105    ;
  38:   ;
  39:  JMP LBL[1000] ;
  40:   ;
  41:   ;
  42:   ;
  43:  !***************************** ;
  44:  !Drop Cases Outbound 1600 ;
  45:  !And Move Home ;
  46:  !***************************** ;
  47:   ;
  48:   ;
  49:  !@PICK ;
  50:  LBL[120] ;
  51:  UFRAME_NUM=1 ;
  52:L PR[226:INIT DEP PK] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  53:  R[250:CUR_POS]=125    ;
  54:   ;
  55:  !@INIT DEP PK ;
  56:  LBL[125] ;
  57:  UFRAME_NUM=1 ;
  58:L PR[229:FINAL DEP PK] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  59:  R[250:CUR_POS]=130    ;
  60:   ;
  61:  !CLEAR OF ALL ;
  62:  R[241:LOC_STATUS]=9999    ;
  63:  GO[10]=R[241:LOC_STATUS] ;
  64:   ;
  65:   ;
  66:  !@FNL DEP PK ;
  67:  !Mov 2 Drop Boxes ;
  68:  LBL[130] ;
  69:  UFRAME_NUM=2 ;
  70:   ;
  71:  CALL HF_PLACE_PRS    ;
  72:   ;
  73:L PR[230:INIT APP PL] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  74:  R[250:CUR_POS]=205    ;
  75:   ;
  76:  !ENTER CONVEYOR 200 ;
  77:  R[241:LOC_STATUS]=201    ;
  78:  GO[10]=R[241:LOC_STATUS] ;
  79:   ;
  80:  !@INIT APP PL ;
  81:  LBL[205] ;
  82:  UFRAME_NUM=2 ;
  83:   ;
  84:  CALL HF_PLACE_PRS    ;
  85:   ;
  86:L PR[233:FINAL APP PL] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
  87:  R[250:CUR_POS]=210    ;
  88:   ;
  89:  !@FNL APP PL ;
  90:  LBL[210] ;
  91:  UFRAME_NUM=2 ;
  92:   ;
  93:  !Check Over Compression @Place ;
  94:  IF RI[4:Flange Prox 1]=OFF AND RI[5:Flange Prox 2]=OFF,JMP LBL[211] ;
  95:   ;
  96:  JMP LBL[220] ;
  97:   ;
  98:  LBL[211] ;
  99:   ;
 100:  !Reset PL Prs & Dpt PL Prs ;
 101:  CALL HF_PLACE_PRS    ;
 102:   ;
 103:  !Over Compression @Place ;
 104:  SKIP CONDITION RI[4:Flange Prox 1]=ON OR RI[5:Flange Prox 2]=ON    ;
 105:   ;
 106:  !Place ;
 107:L PR[234:PLACE] R[257:VEL SETTING]mm/sec FINE ACC R[258] Skip,LBL[778]    ;
 108:   ;
 109:  CALL HF_VAC_OFF    ;
 110:   ;
 111:  JMP LBL[9115] ;
 112:  LBL[778] ;
 113:  R[250:CUR_POS]=220    ;
 114:   ;
 115:  !Update Prev Ht ;
 116:  R[281:Prev Case Ht]=R[215:Box Height]    ;
 117:   ;
 118:   ;
 119:   ;
 120:  !@PLACE/INIT DEP PL ;
 121:  LBL[230] ;
 122:  LBL[220] ;
 123:  !Drop Boxes ;
 124:  CALL HF_VAC_OFF    ;
 125:   ;
 126:  CALL HF_PLACE_PRS    ;
 127:   ;
 128:  !Change Payload to Empty ;
 129:  PAYLOAD[1:Vacuum Gripper] ;
 130:  R[252:CUR_LOAD]=0    ;
 131:   ;
 132:  UFRAME_NUM=2 ;
 133:L PR[236:INIT DEP PL] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
 134:  R[250:CUR_POS]=230    ;
 135:   ;
 136:  UFRAME_NUM=2 ;
 137:L PR[239:FINAL DEP PL] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
 138:  R[250:CUR_POS]=240    ;
 139:   ;
 140:  !EXIT CONVEYOR 200 ;
 141:  R[241:LOC_STATUS]=209    ;
 142:  GO[10]=R[241:LOC_STATUS] ;
 143:   ;
 144:  JMP LBL[1000] ;
 145:   ;
 146:   ;
 147:   ;
 148:  !***************************** ;
 149:  !Move Home From Maint ;
 150:  !***************************** ;
 151:   ;
 152:  LBL[910] ;
 153:  UFRAME_NUM=0 ;
 154:L PR[202:MAINT] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
 155:  R[250:CUR_POS]=910    ;
 156:  JMP LBL[1000] ;
 157:   ;
 158:   ;
 159:   ;
 160:  !***************************** ;
 161:  !Move Home ;
 162:  !***************************** ;
 163:  LBL[240] ;
 164:  LBL[100] ;
 165:  LBL[1000] ;
 166:   ;
 167:  UFRAME_NUM=0 ;
 168:L PR[200:HOME] R[257:VEL SETTING]mm/sec FINE ACC R[258]    ;
 169:  R[250:CUR_POS]=1000    ;
 170:   ;
 171:  CALL HF_VAC_OFF    ;
 172:   ;
 173:  R[241:LOC_STATUS]=9999    ;
 174:  GO[10]=R[241:LOC_STATUS] ;
 175:   ;
 176:  R[252:CUR_LOAD]=0    ;
 177:   ;
 178:  JMP LBL[9999] ;
 179:   ;
 180:  !ERROR LOOP ;
 181:  !Gripper Over Compression ;
 182:  LBL[9115] ;
 183:  R[240:ERROR_STATE]=115    ;
 184:  JMP LBL[9000] ;
 185:   ;
 186:  !ERROR LOOP ;
 187:  LBL[9000:Error] ;
 188:  CALL HF_ERRORLOOP    ;
 189:   ;
 190:  LBL[9999:END] ;
/POS
/END
