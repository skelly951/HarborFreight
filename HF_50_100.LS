/PROG  HF_50_100
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "100 PICK/PLACE";
PROG_SIZE	= 4122;
CREATE		= DATE 21-03-23  TIME 10:53:54;
MODIFIED	= DATE 22-03-02  TIME 16:22:44;
FILE_NAME	= HF_55_10;
VERSION		= 0;
LINE_COUNT	= 230;
MEMORY_SIZE	= 4682;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  OVERRIDE=R[255:SPEED_OVERRIDE] ;
   2:   ;
   3:  !Setup Payload & Frames ;
   4:  PAYLOAD[1:Vacuum Gripper] ;
   5:  UFRAME_NUM=0 ;
   6:  UTOOL_NUM=1 ;
   7:   ;
   8:  CALL HF_INERTIAL_CALC(R[206:CMD_PRM_4_Mass],R[207:CMD_PRM_5_Ht],R[208:CMD_PRM_6_LG],R[209:CMD_PRM_7_WD]) ;
   9:   ;
  10:   ;
  11:  !********************* ;
  12:  !PICK SEQUENCE ;
  13:  !********************* ;
  14:   ;
  15:  !Set PR Parameters ;
  16:  CALL HF_PICK_PRS    ;
  17:  CALL HF_PLACE_PRS    ;
  18:  UFRAME_NUM=1 ;
  19:   ;
  20:  !Request To Enter @Home ;
  21:  IF DI[81:RTE]=ON,JMP LBL[82] ;
  22:   ;
  23:   ;
  24:  !Pick @ Pallet 100 ;
  25:  !ROBOT ENTER PALLET 100 ;
  26:  R[241:LOC_STATUS]=101    ;
  27:  GO[10]=R[241:LOC_STATUS] ;
  28:   ;
  29:  !Initial Approach Pick ;
  30:L PR[220:INIT APP PK] 2500mm/sec CNT100 AP_LD500    ;
  31:  R[250:CUR_POS]=105    ;
  32:   ;
  33:  !Over Compression @FNL Ap Pk ;
  34:  SKIP CONDITION RI[4:Flange Prox 1]=ON OR RI[5:Flange Prox 2]=ON    ;
  35:   ;
  36:  !Final Approach Pick ;
  37:L PR[223:FINAL APP PK] 2500mm/sec CNT50 Skip,LBL[774]    ;
  38:   ;
  39:  PR[275:Skip Cond Pos]=LPOS    ;
  40:  PR[223,3:FINAL APP PK]=PR[275,3:Skip Cond Pos]+100    ;
  41:   ;
  42:  JMP LBL[9115] ;
  43:  LBL[774] ;
  44:   ;
  45:  R[250:CUR_POS]=110    ;
  46:   ;
  47:  !Request To Enter @PrePick ;
  48:  IF DI[81:RTE]=OFF,JMP LBL[81] ;
  49:   ;
  50:  !RTE:Init App PK ;
  51:L PR[220:INIT APP PK] 2500mm/sec CNT100    ;
  52:  R[250:CUR_POS]=105    ;
  53:   ;
  54:  !RTE:ROBOT EXIT PALLET 100 ;
  55:  R[241:LOC_STATUS]=109    ;
  56:  GO[10]=R[241:LOC_STATUS] ;
  57:   ;
  58:  JMP LBL[82] ;
  59:  LBL[81] ;
  60:   ;
  61:   ;
  62:  !Pick Boxes ;
  63:  CALL HF_VAC_ON    ;
  64:   ;
  65:  !Over Compression @Pick ;
  66:  SKIP CONDITION RI[4:Flange Prox 1]=ON OR RI[5:Flange Prox 2]=ON    ;
  67:   ;
  68:  !Pick ;
  69:L PR[224:PICK] 2500mm/sec FINE Skip,LBL[777]    ;
  70:   ;
  71:  PR[275:Skip Cond Pos]=LPOS    ;
  72:  PR[224,3:PICK]=PR[275,3:Skip Cond Pos]+100    ;
  73:  CALL HF_VAC_OFF    ;
  74:   ;
  75:  JMP LBL[9115] ;
  76:  LBL[777] ;
  77:  R[250:CUR_POS]=120    ;
  78:   ;
  79:  R[241:LOC_STATUS]=100    ;
  80:  GO[10]=R[241:LOC_STATUS] ;
  81:   ;
  82:  !Current Payload Case ;
  83:  PAYLOAD[2:Loaded Gripper] ;
  84:  R[252:CUR_LOAD]=1    ;
  85:   ;
  86:  !MITIGATE COL DETECT ;
  87:  COL DETECT OFF ;
  88:   ;
  89:  WAIT    .50(sec) ;
  90:   ;
  91:  !Initial Depart Pick ;
  92:L PR[226:INIT DEP PK] R[257:VEL SETTING]mm/sec CNT10 ACC R[258]    ;
  93:  R[250:CUR_POS]=125    ;
  94:   ;
  95:  !ENBL COL DETECT ;
  96:  COL DETECT ON ;
  97:   ;
  98:  !Final Depart Pick ;
  99:L PR[229:FINAL DEP PK] R[257:VEL SETTING]mm/sec CNT100 ACC R[258]    ;
 100:  R[250:CUR_POS]=130    ;
 101:   ;
 102:  !ROBOT EXIT PALLET 100 ;
 103:  R[241:LOC_STATUS]=109    ;
 104:  GO[10]=R[241:LOC_STATUS] ;
 105:   ;
 106:   ;
 107:  !********************* ;
 108:  !Place Sequence ;
 109:  !********************* ;
 110:   ;
 111:  !Set Place PRs ;
 112:  UFRAME_NUM=2 ;
 113:  CALL HF_PLACE_PRS    ;
 114:   ;
 115:  !CLEAR OF ALL ;
 116:  R[241:LOC_STATUS]=9999    ;
 117:  GO[10]=R[241:LOC_STATUS] ;
 118:   ;
 119:  !Initial Approach Place ;
 120:J PR[230:INIT APP PL] R[258:ACC SETTING]% CNT100 ACC R[258]    ;
 121:  R[250:CUR_POS]=205    ;
 122:   ;
 123:  !ENTER CONVEYOR 200 ;
 124:  R[241:LOC_STATUS]=201    ;
 125:  GO[10]=R[241:LOC_STATUS] ;
 126:   ;
 127:  !Over Compression @FNL Ap PL ;
 128:  SKIP CONDITION RI[4:Flange Prox 1]=ON OR RI[5:Flange Prox 2]=ON    ;
 129:   ;
 130:  !Final Approach Place ;
 131:L PR[233:FINAL APP PL] R[257:VEL SETTING]mm/sec CNT10 AP_LD650 Skip,LBL[775] ACC R[258]    ;
 132:   ;
 133:  PR[275:Skip Cond Pos]=LPOS    ;
 134:  PR[233,3:FINAL APP PL]=PR[275,3:Skip Cond Pos]+100    ;
 135:  CALL HF_VAC_OFF    ;
 136:   ;
 137:  JMP LBL[9115] ;
 138:  LBL[775] ;
 139:   ;
 140:  R[250:CUR_POS]=210    ;
 141:   ;
 142:  !Drop Off Wait - TimeOut ;
 143:  $WAITTMOUT=1000 ;
 144:   ;
 145:  TIMER[2]=RESET ;
 146:  TIMER[2]=START ;
 147:   ;
 148:  LBL[86] ;
 149:  R[397:TimeOut]=TIMER[2]    ;
 150:  IF R[397:TimeOut]>30 AND DI[81:RTE]=ON,JMP LBL[84] ;
 151:   ;
 152:  WAIT DI[83:Ok To Place]=ON TIMEOUT,LBL[86] ;
 153:   ;
 154:  LBL[84] ;
 155:  TIMER[2]=STOP ;
 156:  $WAITTMOUT=3 ;
 157:   ;
 158:  !Reset PL Prs & Dpt PL Prs ;
 159:  CALL HF_PLACE_PRS    ;
 160:   ;
 161:  !Over Compression @Place ;
 162:  SKIP CONDITION RI[4:Flange Prox 1]=ON OR RI[5:Flange Prox 2]=ON    ;
 163:   ;
 164:  !Place ;
 165:L PR[234:PLACE] 750mm/sec FINE Skip,LBL[778] ACC R[258]    ;
 166:   ;
 167:  PR[275:Skip Cond Pos]=LPOS    ;
 168:  PR[234,3:PLACE]=PR[275,3:Skip Cond Pos]+100    ;
 169:  CALL HF_VAC_OFF    ;
 170:   ;
 171:  JMP LBL[9115] ;
 172:  LBL[778] ;
 173:  R[250:CUR_POS]=220    ;
 174:   ;
 175:   ;
 176:  !Update Prev Ht ;
 177:  R[281:Prev Case Ht]=R[215:Box Height]    ;
 178:   ;
 179:  R[241:LOC_STATUS]=200    ;
 180:  GO[10]=R[241:LOC_STATUS] ;
 181:   ;
 182:  !Drop Product and Check Vacuum ;
 183:  CALL HF_VAC_OFF    ;
 184:   ;
 185:  !RESET TIMER ;
 186:  R[384]=TIMER[1]    ;
 187:  TIMER[1]=RESET ;
 188:   ;
 189:  !Change Payload to Empty ;
 190:  PAYLOAD[1:Vacuum Gripper] ;
 191:  R[252:CUR_LOAD]=0    ;
 192:   ;
 193:  !Initial Depart Place ;
 194:L PR[236:INIT DEP PL] 2500mm/sec CNT100    ;
 195:  R[250:CUR_POS]=230    ;
 196:   ;
 197:  !Final Depart Place ;
 198:L PR[239:FINAL DEP PL] 2500mm/sec CNT100    ;
 199:  R[250:CUR_POS]=240    ;
 200:   ;
 201:  !EXIT CONVEYOR 200 ;
 202:  R[241:LOC_STATUS]=209    ;
 203:  GO[10]=R[241:LOC_STATUS] ;
 204:   ;
 205:  !Request To Enter @PostPick ;
 206:  IF DI[81:RTE]=OFF,JMP LBL[9999] ;
 207:   ;
 208:  !Move Home if RTE Active ;
 209:  LBL[82] ;
 210:  UFRAME_NUM=0 ;
 211:L PR[200:HOME] 2500mm/sec FINE    ;
 212:  R[250:CUR_POS]=1000    ;
 213:   ;
 214:  !CLEAR OF ALL ;
 215:  R[241:LOC_STATUS]=9999    ;
 216:  GO[10]=R[241:LOC_STATUS] ;
 217:   ;
 218:  JMP LBL[9999] ;
 219:   ;
 220:  !ERROR LOOP ;
 221:  !Gripper Over Compression ;
 222:  LBL[9115] ;
 223:  R[240:ERROR_STATE]=115    ;
 224:  JMP LBL[9000] ;
 225:   ;
 226:  LBL[9000:Error] ;
 227:  CALL HF_ERRORLOOP    ;
 228:  JMP LBL[9999] ;
 229:   ;
 230:  LBL[9999:End] ;
/POS
/END
