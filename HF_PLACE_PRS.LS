/PROG  HF_PLACE_PRS
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "Offset Drop Ht";
PROG_SIZE	= 2375;
CREATE		= DATE 21-04-30  TIME 08:36:16;
MODIFIED	= DATE 22-04-30  TIME 11:19:06;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 121;
MEMORY_SIZE	= 2787;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !********************* ;
   2:  !Set Place Positions ;
   3:  !********************* ;
   4:   ;
   5:  UFRAME_NUM=0 ;
   6:  CALL SET_OFFS(0,0,0,(-180),0,0,1) ;
   7:   ;
   8:  PR[265:1600 UF2 Offset]=PR[264:1600 Master Plae]+PR[1:UFrame Offset]    ;
   9:   ;
  10:  UFRAME[2]=PR[265:1600 UF2 Offset] ;
  11:   ;
  12:  !UserFrame 2 ;
  13:  UFRAME_NUM=2 ;
  14:   ;
  15:  !Check for Valid Prev Case Ht ;
  16:  IF DI[83:Ok To Place]=OFF AND R[281:Prev Case Ht]=0,JMP LBL[5] ;
  17:   ;
  18:  JMP LBL[6] ;
  19:  LBL[5] ;
  20:   ;
  21:  R[281:Prev Case Ht]=R[215:Box Height]    ;
  22:   ;
  23:  LBL[6] ;
  24:   ;
  25:  R[396:Z Offset Temp]=R[215:Box Height]+R[281:Prev Case Ht]+R[282:Clearance Buffer]    ;
  26:   ;
  27:   ;
  28:  !@Fnl Dpt Pk : Set App PL Prs ;
  29:   ;
  30:  IF DI[83:Ok To Place]=ON,JMP LBL[20] ;
  31:   ;
  32:   ;
  33:  !Check Obstructed Drop Off ;
  34:  !...and Rail Clearance ;
  35:  !...for optimal approach ;
  36:  IF R[281:Prev Case Ht]>=R[279:Drop Rail Clear],JMP LBL[10] ;
  37:   ;
  38:  R[396:Z Offset Temp]=R[280]+R[279:Drop Rail Clear]+R[282:Clearance Buffer]    ;
  39:   ;
  40:  JMP LBL[11] ;
  41:  LBL[10] ;
  42:   ;
  43:  R[396:Z Offset Temp]=R[280]+R[281:Prev Case Ht]+R[282:Clearance Buffer]    ;
  44:   ;
  45:  LBL[11] ;
  46:   ;
  47:  JMP LBL[21] ;
  48:  LBL[20] ;
  49:   ;
  50:  R[396:Z Offset Temp]=R[280]+R[279:Drop Rail Clear]+R[282:Clearance Buffer]    ;
  51:   ;
  52:  LBL[21] ;
  53:   ;
  54:   ;
  55:  !Set Approach PL Prs ;
  56:   ;
  57:  !Initial App PL ;
  58:  CALL SET_OFFS(R[287:PL Offset:X(UF2)],R[288:PL Offset:Y(UF2)],R[396:Z Offset Temp],180,0,0,1) ;
  59:  PR[230:INIT APP PL]=PR[1:UFrame Offset]    ;
  60:   ;
  61:  !Final App PL ;
  62:  CALL SET_OFFS(0,0,R[396:Z Offset Temp],180,0,0,1) ;
  63:  PR[233:FINAL APP PL]=PR[1:UFrame Offset]    ;
  64:   ;
  65:   ;
  66:   ;
  67:   ;
  68:  !@Fnl Apr PL : Set Place ;
  69:   ;
  70:  !Check Obstructed Drop Off ;
  71:  !...for double stack ;
  72:  IF DI[83:Ok To Place]=ON,JMP LBL[12] ;
  73:   ;
  74:  R[396:Z Offset Temp]=R[215:Box Height]+R[281:Prev Case Ht]+R[277:Place Buffer]    ;
  75:   ;
  76:  !Set Place - HIGH ;
  77:  CALL SET_OFFS(0,0,R[396:Z Offset Temp],180,0,0,1) ;
  78:  PR[234:PLACE]=PR[1:UFrame Offset]    ;
  79:   ;
  80:  !Init Dpt Pl - HIGH ;
  81:  R[396:Z Offset Temp]=1576    ;
  82:  CALL SET_OFFS(0,0,R[396:Z Offset Temp],180,0,0,1) ;
  83:  PR[236:INIT DEP PL]=PR[1:UFrame Offset]    ;
  84:   ;
  85:  !Final Dpt Pl - HIGH ;
  86:  CALL SET_OFFS(0,0,0,0,0,45,1) ;
  87:  PR[239:FINAL DEP PL]=PR[230:INIT APP PL]+PR[1:UFrame Offset]    ;
  88:   ;
  89:  JMP LBL[13] ;
  90:  LBL[12] ;
  91:   ;
  92:  R[396:Z Offset Temp]=R[215:Box Height]+R[277:Place Buffer]    ;
  93:   ;
  94:  !Set Place - LOW ;
  95:  CALL SET_OFFS(0,0,R[396:Z Offset Temp],180,0,0,1) ;
  96:  PR[234:PLACE]=PR[1:UFrame Offset]    ;
  97:   ;
  98:  !Check Departure Rail Clearance ;
  99:  IF R[215:Box Height]>=R[279:Drop Rail Clear],JMP LBL[14] ;
 100:   ;
 101:  R[396:Z Offset Temp]=R[215:Box Height]+R[279:Drop Rail Clear]+R[282:Clearance Buffer]    ;
 102:   ;
 103:  JMP LBL[15] ;
 104:  LBL[14] ;
 105:   ;
 106:  R[396:Z Offset Temp]=R[215:Box Height]+R[282:Clearance Buffer]    ;
 107:   ;
 108:  LBL[15] ;
 109:   ;
 110:  !Init Dpt Pl - LOW ;
 111:  CALL SET_OFFS(0,0,R[396:Z Offset Temp],180,0,0,1) ;
 112:  PR[236:INIT DEP PL]=PR[1:UFrame Offset]    ;
 113:   ;
 114:  !Final Dpt Pl - LOW ;
 115:  CALL SET_OFFS(R[287:PL Offset:X(UF2)],R[288:PL Offset:Y(UF2)],R[396:Z Offset Temp],180,0,45,1) ;
 116:  PR[239:FINAL DEP PL]=PR[1:UFrame Offset]    ;
 117:   ;
 118:  LBL[13] ;
 119:   ;
 120:   ;
 121:  LBL[9999] ;
/POS
/END
